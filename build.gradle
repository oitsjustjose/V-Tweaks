buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
    dependencies {
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0+"
}

repositories { maven { url = "https://dvs1.progwml6.com/files/maven" } }

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

group = 'com.oitsjustjose.vtweaks' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "VTweaks-${game_version}-${mod_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
minecraft {
    mappings channel: 'official', version: "${game_version}"
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { vtweaks { source sourceSets.main } }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { vtweaks { source sourceSets.main } }
        }
    }
}


dependencies {
    minecraft "net.minecraftforge:forge:${game_version}-${forge_version}"
    compileOnly fg.deobf("mezz.jei:jei-${game_version}-common-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${game_version}-common:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${game_version}-forge-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${game_version}-forge:${jei_version}")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, "vtweaks.refmap.json"
    config "vtweaks.mixins.json"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "vtweaks",
                "Specification-Vendor"    : "oitsjustjose.com",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "oitsjustjose.com",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.register('publish') {
    dependsOn 'curseforge'
    dependsOn 'modrinth'
}

curseforge {
    apiKey = project.hasProperty("CURSEFORGE_API_KEY") ? project.CURSEFORGE_API_KEY : ""
    project {
        id = '238048'
        changelogType = 'markdown'
        changelog = file("./changelog.md")
        releaseType = 'release'
        addGameVersion "${game_version}"
        mainArtifact(jar) {
            displayName = "V-Tweaks ${game_version} ${mod_version}"
        }
    }
}

modrinth {
    token = project.hasProperty("MODRINTH_API_KEY") ? project.MODRINTH_API_KEY : ""
    projectId = "Bmda4Rq4"
    versionName = "V-Tweaks ${game_version} ${mod_version}"
    versionNumber = "${game_version}-${mod_version}"
    versionType = "release"
    uploadFile = jar
    gameVersions = ["${game_version}"]
    loaders = ["forge"]
    changelog = file("./changelog.md").text
    dependencies { required.project "jei" }
}
