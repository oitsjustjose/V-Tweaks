plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0+"
    id 'net.neoforged.gradle.userdev' version '7.0.26'
}

repositories { maven { url = "https://maven.blamejared.com" } }

group = 'com.oitsjustjose.vtweaks'
archivesBaseName = "VTweaks-${game_version}-${mod_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

//minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')
//minecraft.accessTransformers.entry public net.minecraft.client.Minecraft textureManager # textureManager

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', "vtweaks"
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', "vtweaks"
        programArgument '--nogui'
    }
}

dependencies {
    implementation "net.neoforged:forge:${game_version}-${neo_version}"
    compileOnly "mezz.jei:jei-${game_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${game_version}-forge-api:${jei_version}"
//    runtimeOnly "mezz.jei:jei-${game_version}-common:${jei_version}"
//    runtimeOnly "mezz.jei:jei-${game_version}-forge:${jei_version}"
}

jar {
    manifest {
        attributes([
            "Specification-Title"     : "vtweaks",
            "Specification-Vendor"    : "oitsjustjose.com",
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${mod_version}",
            "Implementation-Vendor"   : "oitsjustjose.com",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }

tasks.register('publish') {
    dependsOn 'curseforge'
    dependsOn 'modrinth'
}

curseforge {
    apiKey = project.hasProperty("CURSEFORGE_API_KEY") ? project.CURSEFORGE_API_KEY : ""
    project {
        id = '238048'
        changelogType = 'markdown'
        changelog = file("./changelog.md")
        releaseType = 'release'
        addGameVersion "${game_version}"
        addGameVersion "forge"
        addGameVersion "neoforge"
        mainArtifact(jar) {
            displayName = "V-Tweaks ${game_version} ${mod_version}"
        }
    }
}

modrinth {
    token = project.hasProperty("MODRINTH_API_KEY") ? project.MODRINTH_API_KEY : ""
    projectId = "Bmda4Rq4"
    versionName = "V-Tweaks ${game_version} ${mod_version}"
    versionNumber = "${game_version}-${mod_version}"
    versionType = "release"
    uploadFile = jar
    gameVersions = ["${game_version}"]
    loaders = ["forge", "neoforge"]
    changelog = file("./changelog.md").text
    dependencies { required.project "jei" }
}
